;;; mantle/config/spotify.el --- MUSIC! -*- lexical-binding: t; -*-
;;
;; Author:     Cole Brown <http://github/cole-brown>
;; Maintainer: Cole Brown <code@brown.dev>
;; Created:    2023-04-28
;; Modified:   2023-04-28
;; URL:        https://github.com/cole-brown/.config-emacs
;;
;; These are not the GNU Emacs droids you're looking for.
;; We can go about our business.
;; Move along.
;;
;;; Commentary:
;;
;;  Configure Smudge, which is a Spotify controller.
;;
;;; Code:


;;------------------------------------------------------------------------------
;; Unicode Media Control Symbols
;;------------------------------------------------------------------------------
;; - https://en.wikipedia.org/wiki/Media_control_symbols

;; NOTE: They're not a width that plays nice with monospaced fonts...
;; Hence the tabs for alignment...
;;
;;   U+23F5  ‚ñ∂Ô∏è/‚èµ 	 U+25B6 ‚ñ∂/‚ñ∂ 		 Play
;;   U+23F8  ‚è∏/‚è∏ 	        		 Pause
;;   U+23EF  ‚èØ/‚èØ 	        		 Play/Pause toggle
;;   U+23F4  ‚è¥ 	 U+25C0 ‚óÄ/‚óÄ 		 Reverse
;;   U+23F9  ‚èπ 	 U+25A0 ‚ñ† 		 Stop
;;   U+23EA  ‚è™/‚è™ 	        		 Back (Fast Backwards)
;;   U+23E9  ‚è©/‚è© 	        		 Fast forward
;;   U+23EE  ‚èÆ/‚èÆ 	        		 Skip to the start or previous file/track/chapter
;;   U+23ED  ‚è≠/‚è≠ 	        		 Skip to the end or next file/track/chapter
;;   U+23FA  ‚è∫/‚è∫ 	        		 Record
;;   U+23CF  ‚èè/‚èè 	        		 Eject
;;   U+1F500 üîÄ/üîÄ 	        		 Shuffle
;;   U+1F501 üîÅ/üîÅ 	U+1F503 üîÉ/üîÉ 	 Repeat (indefinitely)
;;   U+1F502 üîÇ/üîÇ 	        		 Repeat once
;;   U+2139  ‚Ñπ/‚Ñπ 	        		 Info
;;   U+1F504 üîÑ/üîÑ 	        		 Reload, Refresh
;; See also:
;;   http://xahlee.info/comp/unicode_computing_symbols.html


;;------------------------------------------------------------------------------
;; Smudge/Spotify Helpers
;;------------------------------------------------------------------------------
;; TODO:smudge: Make a "smudge-api.el" file and do a pull request?
;;   - Well, there already is a file named that in my smudge repo so... Put these there or some other file?

(defun int<smudge>:response (keyword msg &rest args)
  "Print MSG and ARGS via `message' for a response from a `smudge' async func.

Prefix messages with \"Spotify: \"

Return KEYWORD"
  (when msg
    (message "Spotify: %s"
             (if args
                 (apply #'format msg args)
               msg)))
  keyword)


(defun async<smudge>:device:select (&optional name)
  "Select device NAME (default `(system-name)') as the active Spotify device.

TODO:smudge: Better error handling from Smudge would be nice...
Prints a(n error) message if:
  - NAME is not a string.
  - Spotify user does not have a premium subscription.
  - NAME is not in Spotify's list of devices.
  - NAME not found on the list of Spotify devices.

TODO:smudge: Better error handling from Smudge would be nice...
             Am I allowed to raise error signals?

Returns `:error' or nil."
  (let ((desired:name (or name (system-name)))
        ;; Did we end up finding a device to try to select?
        (desired:active? nil))
    (if (not (stringp desired:name))
        (int<smudge>:response :error
                              "Desired device name must be a string, got: NAME: %S, DESIRED:NAME: %S"
                              name
                              desired:name)

      ;; Check the Spotify user has enough permissions.
      (smudge-api-current-user
       (lambda (user)
         (if (not (string= (gethash 'product user) "premium"))
             (int<smudge>:response :error
                                   "This feature requires a Spotify premium subscription.")

           ;; Get devices currently known to Spotify.
           (smudge-api-device-list
            (lambda (json)
              (if-let ((devices (gethash 'devices json)))
                  (dolist (device devices)
                    (when-let* ((device:name    (smudge-device-get-device-name device))
                                (device:id      (smudge-device-get-device-id device))
                                (device:select? (and (stringp device:name)
                                                     (string= device:name desired:name))))
                      ;; Note the state we found it in.
                      (setq desired:active? (smudge-device-get-device-is-active device))

                      ;;------------------------------
                      ;; Found a matching device; select it.
                      ;;------------------------------
                      (smudge-api-transfer-player
                       device:id
                       (lambda (_)
                         (setq smudge-selected-device-id device:id)
                         (if desired:active?
                             (int<smudge>:response nil
                                                   "Device '%s' already active. Reselected anyways."
                                                   device:name)
                           (int<smudge>:response nil
                                                 "Device '%s' selected."
                                                 device:name))))))

                (int<smudge>:response :error
                                      "No devices are available."))))
           nil))))))
;; (async<smudge>:device:select)


(defun async<smudge>:device:active? (name callback)
  "Call CALLBACK with nil/non-nil result of checking if device NAME is active.

CALLBACK should be a function with a signature like:
  - (defun CALLBACK (boolean) ...)
  - (lambda (boolean) ...)
That is, it receives a nil/non-nil result for whether the Spotify device NAME is
active.

The CALLBACK's nil/non-nil input is based on NAME, which should be nil,
`:any', or a string:
  - If NAME is a string, it is whether that exact Spotify device name is active.
  - If `:any', it is whether _any_ Spotify device is active.
  - If nil, it is whether \"this\" Spotify device is active.
    - Where \"this\" is what the string return of function `system-name' names.

This function always returns nil. Your CALLBACK gets the result."
  (let ((desired:name (or name
                          (system-name))))
    ;;------------------------------
    ;; Error Checks
    ;;------------------------------
    ;; Invalid CALLBACK?
    (cond ((not (functionp callback))
           (error "async<smudge>:device:active?: CALLBACK must be a function. CALLBACK: %S"
                  callback))

          ;; Invalid NAME input?
          ((not (or (eq desired:name :any)
                    (stringp desired:name)))
           (funcall callback
                    (int<smudge>:response nil
                                          "Desired device name must be %s, got: NAME: %S, DESIRED:NAME: %S"
                                          "a string or `:any'"
                                          name
                                          desired:name)))
          ;;------------------------------
          ;; Check if device is active.
          ;;------------------------------
          ;; Make the async Smudge calls.
          (t
           ;; Check the Spotify user has enough permissions.
           (smudge-api-current-user
            (lambda (user)
              (if (not (string= (gethash 'product user) "premium"))
                  (funcall callback
                           (int<smudge>:response nil
                                                 "This feature requires a Spotify premium subscription."))

                ;; Get devices currently known to Spotify.
                (smudge-api-device-list
                 (lambda (json)
                   (let ((devices (gethash 'devices json))
                         ;; "Don't care about the rest." flag
                         device:found
                         ;; CALLBACK's input.
                         devices:active)
                     (dolist (device devices)
                       ;; Only bother when we need to keep searching and we've got an active device to check.
                       (when-let ((keep-searching (not device:found))
                                  (device:name    (smudge-device-get-device-name device))
                                  (device:id      (smudge-device-get-device-id device))
                                  (device:active  (smudge-device-get-device-is-active device)))
                         ;; Any device? Push active device names to CALLBACK's input.
                         (cond ((eq desired:name :any)
                                (push device:name devices:active))

                               ;; Specific device check.
                               ((and (stringp device:name)
                                     (string= device:name desired:name))
                                ;; Mark that we don't care about the rest.
                                (setq device:found t)
                                (push device:name devices:active))

                               ;; Don't care about this device.
                               (t
                                nil))))

                     ;; Return result.
                     (if devices:active
                         (funcall callback devices:active)
                       (funcall callback
                                (int<smudge>:response nil
                                                      "No devices are active."))))))))))))

  ;; Always return nil so I don't confuse myself again with this async func.
  nil)
;; (async<smudge>:device:active? nil (lambda (active?) (message "active? %S" active?)))
;; (async<smudge>:device:active? :any (lambda (active?) (message "active? %S" active?)))


(defun async<smudge>:device:activate (name)
  "Tell Spotify to transfer to/activate device NAME.

Always returns nil."
  ;; TODO:smudge: input validity check
  (smudge-api-device-list
   (lambda (json)
     "Activate NAME device from list, if possible."
     (let (names/known)
       (if-let* ((devices     (gethash 'devices json))
                 (device      (pop devices))
                 (continue    t))
           ;;------------------------------
           ;; Got one or more devices; process them.
           ;;------------------------------
           (progn
             (while (and device continue)
               ;; Only bother if we have the vars and it's the device name we want.
               (when-let ((device/name   (smudge-device-get-device-name device))
                          (device/id     (smudge-device-get-device-id device))
                          (device/active (smudge-device-get-device-is-active device))
                          (activate?     (string= name device/name)))
                 ;; Add to list of known devices.
                 (if (eq names/known t)
                     (setq names/known (list device/name))
                   (push device/name names/known))

                 (if device/active
                     ;; Already active - just say that?
                     (progn
                       ;; NOTE: Could add a "force activate" param or just ignore
                       ;; `device/active' if there are bugs/issues that need worked
                       ;; around, but this seems smart right now?
                       (int<smudge>:response name
                                             "Device '%s' already active."
                                             name)
                       (setq continue nil))

                   ;; Activate (transfer to) this desired device.
                   (smudge-api-transfer-player
                    device/id
                    ;; This is what Smudge does in `smudge-device-select-active', so I think we do it here?
                    (lambda (json)
                      (setq smudge-selected-device-id device-id)
                      (int<smudge>:response name
                                            "Device '%s' selected" name)))
                   ;; We've found and activated the device; stop the loop.
                   (setq continue nil)))

               ;; Finish up by selecting next device to process.
               (setq device (pop devices)))

             ;;------------------------------
             ;; Feedback: Did we activate anything?
             ;;------------------------------
             (if continue
                 ;; Device requested wasn't in the list.
                 (int<smudge>:response nil
                                       "Desired device '%s' isn't available right now. Available devices: %s"
                                       name
                                       (or names/known "none"))
               (int<smudge>:response name
                                     "Active Device: '%s'."
                                     name)))

         ;;------------------------------
         ;; No devices from Spotify!
         ;;------------------------------
         (int<smudge>:response nil
                               "No devices.")))))

  ;; Async function - always return nil.
  nil)


(defun async<smudge>:init (&rest plist)
  "Initialize Smudge & Spotify, as necessary, using PLIST.

PLIST can have:
  - `:smudge-mode'
    - non-nil (default if key not supplied)
      - Enables `global-smudge-remote-mode' if needed.
    - nil
      - Don't bother `global-smudge-remote-mode'.

  - `:device'
    - nil (default if key not supplied)
      - Activate function `system-name' return value as the Spotify device.
    - `preserve'
      - If any device is active, leave it. Otherwise activate function
        `system-name'.
    - STRING
      - Activate STRING as the Spotify device."
  (let* ((func/name "async<smudge>:init")
         (enable/smudge-mode (if (plist-member plist :smudge-mode)
                                 (plist-get plist :smudge-mode)
                               ;; Default to enabling the mode if no key supplied.
                               :enable))
         (device/this        (system-name))
         (device/valid       '("DEVICE-NAME-STRING" device nil)) ;; Only for printed in error message.
         (enable/device      (if (plist-member plist :device)
                                 (plist-get plist :device)
                               ;; Default to activating this system if no key supplied.
                               device/this)))

    ;;------------------------------
    ;; Activate Device
    ;;------------------------------
    ;; If anything is active, ok. Otherwise activate this device.
    (cond ((eq enable/device 'preserve)
           (async<smudge>:device:active? :any
                                         (lambda (active)
                                           "Activate this device if any device is not ACTIVE."
                                           (unless active
                                             (async<smudge>:device:activate device/this)))))

          ;; Enable a specific device by name.
          ((stringp enable/device)
           ;; Activate if not active already.
           (async<smudge>:device:activate device/this))

          ;; Already converted `nil' into `system-name' string, so...
          ;; Don't know; error.
          (t
           (error "%s: Unknown `:device' option! Allowed: %S, Got: %S"
                  func/name device/valid enable/device)))

    ;;------------------------------
    ;; Global Smudge Remote Mode
    ;;------------------------------
    (when enable/smudge-mode
      (global-smudge-remote-mode 1))
    ;; Just ignore `enable/smudge-mode' == nil; don't disable the mode in an init func.
    ))


(defmacro async<smudge>:device:when-active (desired-name &rest body)
  "Evaluate BODY when DESIRED-NAME Spotify device is active.

DESIRED-NAME must be a string of the Spotify device's name.

Variables available during BODY:
  - `macro:devices'       - list of all devices
  - `macro:device'        - desired device
  - `macro:device/name'   - DESIRED-NAME, but from Spotify
  - `macro:device/id'     - device's ID
  - `macro:device/active' - boolean

Always returns nil."
  (declare (indent 1))
  `(progn
     (smudge-api-device-list
      (lambda (macro:json)
        "Run the BODY if DESIRED-NAME is active."
        (when-let* ((macro:desired-name ,desired-name)
                    (macro:devices      (gethash 'devices macro:json))
                    (macro:device       (pop macro:devices))
                    (macro:continue     t))
          ;;------------------------------
          ;; Got one or more devices; process them.
          ;;------------------------------
          (while (and macro:device macro:continue)
            ;; Only bother if we have the vars and it's the device name we want.
            (when-let ((macro:device/name   (smudge-device-get-device-name macro:device))
                       (macro:device/id     (smudge-device-get-device-id macro:device))
                       (macro:device/active (smudge-device-get-device-is-active macro:device))
                       (macro:run-body?     (string= macro:desired-name macro:device/name)))
              ;; Run it.
              (setq macro:continue nil)
              ,@body)

            ;; Selecting next device to process in loop.
            (setq macro:device (pop macro:devices))))))
     nil))
;; (async<smudge>:device:when-active (system-name) (message "hello there"))


;;------------------------------------------------------------------------------
;; Smudge (Spotify Controller)
;;------------------------------------------------------------------------------

(defvar innit:path:package:smudge (path:join innit:path:packages:user "smudge")
  "`use-package' doesn't like having a function call for `:load-path', thus this.")


;; TODO:smudge: Why isn't Smudge installing/loading its requirements? Because it's a local repo???
;;   > Package-Requires: ((emacs "27.1") (simple-httpd "1.5") (request "0.3") (oauth2 "0.16"))
(imp:use-package simple-httpd)
(imp:use-package request)
(imp:use-package oauth2)


;; https://github.com/danielfm/smudge/
(imp:use-package smudge
  ;;---
  ;; Git Repo, Local & Unmanaged
  ;;---
  ;; This is my own fork of the repo for this package, so...
  ;;   1) Don't try to install.
  :ensure nil
  ;;   2) Here's where it is; add this dir to the `load-path'.
  :load-path innit:path:package:smudge

  ;;---
  ;; Git Repo, Managed by Straight
  ;;---
  ;; Or we could use straight if we don't care about being in the middle of
  ;; whatever branch we happen to be working on:
  ;; :straight (:type git
  ;;            :host github
  ;;            :repo "cole-brown/smudge")
  ;;            ;; :repo "danielfm/smudge")

  ;;---
  ;; Normal Package Management
  ;;---
  ;; Or we could get it from MELPA by just not saying anything.

  ;;------------------------------
  :custom
  ;;------------------------------

  ;;---
  ;; Connection Settings
  ;;---
  (smudge-transport 'connect)
  (smudge-oauth2-callback-port "14576")
  (smudge-oauth2-client-id     (plist-get secret:key:spotify :id))
  (smudge-oauth2-client-secret (plist-get secret:key:spotify :secret))

  ;; This has to make an API call every time, so... don't overdo it and throttle yourself.
  (smudge-player-status-refresh-interval 10) ; Refresh rate in seconds.

  ;;---
  ;; Display
  ;;---

  ;; Put status in title-bar instead of modeline
  (smudge-status-location 'title-bar)

  ;; smudge-player-status-format: default is "[%p: %a - %t ‚ó∑ %l %r%s]"
  ;;   - https://github.com/danielfm/smudge#customizing-the-player-status
  (smudge-player-status-format
   ;; TODO:smudge: Elapsed/current time?
   "%r%s%p ‚ï†‚ïê %a ‚ïê‚ï¨‚ïê %t ‚ïê‚ï£ ‚ó∑%l")
  (smudge-title-bar-separator "  ‚îÖ‚îÖ  ")
  ;; frame-title-format

  ;; TODO:smudge: Is this long enough?
  ;; I got a lot more room in the titlebar than the modeline...
  (smudge-player-status-truncate-length 30) ;; default: 15

  ;;---
  ;; Status Text
  ;;---
  ;; Defaults are a grab-bag of words ("Playing"/"Stopped") and letters ("R"/"-").

  ;; Do I have unicode media icons?
  ;;   Play:         ‚ñ∂ / ‚ñ∂Ô∏è
  ;;   Pause:        ‚è∏ / ‚è∏Ô∏è
  ;;   Play/Pause:   ‚èØ / ‚èØÔ∏è
  ;;   Stop:         ‚èπ / ‚èπÔ∏è
  ;;   Skip Back:    ‚èÆ / ‚èÆÔ∏è
  ;;   Skip Forward: ‚è≠ / ‚è≠Ô∏è
  ;;   Shuffle:      üîÄ / üîÄÔ∏è
  ;;   Repeat:       üîÅ / üîÅÔ∏è
  ;;   'Blank/Off':  ‚óª

  ;; Unicode Icons:
  (smudge-player-status-playing-text       "‚ñ∂Ô∏è")
  (smudge-player-status-paused-text        "‚è∏")
  (smudge-player-status-stopped-text       "‚èπ")
  (smudge-player-status-repeating-text     "üîÅ")
  (smudge-player-status-not-repeating-text "")   ;; "‚óªÔ∏è")
  (smudge-player-status-shuffling-text     "üîÄ")
  (smudge-player-status-not-shuffling-text "")   ;; "‚óª")

  ;; ASCII:
  ;; (smudge-player-status-playing-text       "p")
  ;; (smudge-player-status-paused-text        "-")
  ;; (smudge-player-status-stopped-text       "x")
  ;; (smudge-player-status-repeating-text     "R")
  ;; (smudge-player-status-not-repeating-text "-")
  ;; (smudge-player-status-shuffling-text     "S")
  ;; (smudge-player-status-not-shuffling-text "-")


  ;;------------------------------
  :config
  ;;------------------------------

  (defun mantle:workday:end/spotify ()
    "Stop Smudge & do any needed clean up so Emacs doesn't freeze overnight.

Pause Spotify and close Smudge.

Smudge used to end up with zombie connections when left open overnight... Don't
know if it still does, but this was the solution:
  - Stop/pause Spotify
  - Turn off global Smudge minor mode"
    (interactive)

    (let ((progress-reporter (make-progress-reporter (format "[%s] Spotify clean-up..."
                                                             (datetime:string/get 'rfc-3339 'datetime))
                                                     0
                                                     100)))

      (async<smudge>:device:when-active
          (system-name)
        ;;---
        ;; [2022-02-18] Cleaner Version?
        ;;---
        (smudge-connect-player-pause)
        (progress-reporter-update progress-reporter 50)

        ;; TODO:smudge: Do I need to clean up any buffers?
        ;; (buffer:kill:matching ...)?

        ;; ;;---
        ;; ;; [2019-10-22] Ye Olde Version
        ;; ;;   Do I need the condition case still?
        ;; ;;---
        ;; (condition-case-unless-debug err
        ;;   (progn
        ;;     (smudge-connect-player-pause)
        ;;     (global-smudge-remote-mode -1)
        ;;     ;; TODO:smudge: Do I need to clean up any buffers?
        ;;     ;; (buffer:kill:matching ...)?
        ;;     )
        ;; ;; Catch signaled error 'error': downgrade to just message.
        ;; ;; [2019-10-22]: This is just theory as spotify can get cranky if
        ;; ;; connected but device was left paused...
        ;; (error
        ;;  ;; Downgrade.
        ;;  (message "[ERROR]: mantle:workday:end/spotify: Received error signal:" err)))
        )

      ;; Always disable Smudge mode?
      (global-smudge-remote-mode -1)
      (progress-reporter-done progress-reporter)))
  ;; (mantle:workday:end/spotify)


  ;; TODO:smudge: Is this hack still required?
  ;; HACK: This is a defconst so it doesn't update when you set a different port number.
  ;; So... Just force it to update.
  (setq smudge-api-oauth2-callback (concat "http://localhost:" smudge-oauth2-callback-port "/" smudge-oauth2-callback-endpoint)))


;;--------------------------------------------------------------------------------
;; Keybinds : Meow
;;--------------------------------------------------------------------------------

(imp:use-package smudge
  ;;---
  ;; Package Source
  ;;---
  ;; NOTE: Use same package source as above?
  ;; This is my own fork of the repo for this package, so...
  ;;   1. Don't try to install.
  :ensure nil
  ;;   2. Here's where it is; add this dir to the `load-path'.
  :load-path innit:path:package:smudge

  ;;---
  ;; Load Conditions/Ordering
  ;;---
  :when  (imp:flag? :keybinds +meow)
  :after (:and meow pretty-hydra)


  ;;------------------------------
  :config
  ;;------------------------------

  (defvar int<smudge>:hydra/title
    ;; TODO: Need any height/v-adjust?
    (mantle:user:icon/font-awesome "spotify" ;; Font Awesome icon name
                                   "Spotify" ;; Text after icon
                                   ;; Icon Settings
                                   :color:icon "limegreen"
                                   ;; :height 1
                                   ;; :v-adjust -0.05
                                   )
    "Propertized string with icon & name for Spotify pretty-hydra.")
  ;; int<smudge>:hydra/title
  ;; (insert int<smudge>:hydra/title)


  (defvar int<smudge>:hydra/title:status/style 'detailed
    "Style of Smudge status in the hydra title.

 Options:
  - `status'   - Use the short `smudge-controller-player-status' string.
  - `detailed' - Detailed table of status.
  - nil        - No status in the title.")


  (defun int<smudge>:title:volume/meter (percentage)
    "Return volume PERCENTAGE [0,100] integer as a unicode meter.

Examples:
  - percentage -> returned string
  -   0 -> \"‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä\"
  -   4 -> \"‚£Ü‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä\"
  -  42 -> \"‚£ø‚£ø‚£ø‚£ø‚£Ñ‚£Ä‚£Ä‚£Ä‚£Ä‚£Ä\"
  - 100 -> \"‚£ø‚£ø‚£ø‚£ø‚£ø‚£ø‚£ø‚£ø‚£ø‚£ø\""
    ;;------------------------------
    ;; Error Checks
    ;;------------------------------
    (unless (and (integerp percentage)
                 (< -1 percentage 101))
      (error "int<smudge>:title:volume/meter: PERCENTAGE must be integer in range [0,100]; got %s: %S"
             (type-of percentage)
             percentage))

    ;;------------------------------
    ;; +Progress+ Volume Meter
    ;;------------------------------
    (let* ((meter/length 10)
           (meter/char/full ?‚£ø)
           (meter/char/empty ?‚£Ä)
           ;; NOTE: The bottom 2 dots/pips don't "count" - they're `meter/char/empty', after all.
           (meter/char/partials '(?‚£Ñ ?‚£Ü ?‚£á ?‚£ß ?‚£∑ ?‚£ø))
           ;; Percentage value of one of the 6 dots/pips used in `meter/char/partials'.
           (meter/char/partials/value (/ (float meter/length)
                                         (length meter/char/partials)))
           output)
      ;;---
      ;; "Full Volume" characters
      ;;---
      (while (>= percentage 10)
        (push meter/char/full output)
        (setq percentage (- percentage 10)))

      ;;---
      ;; "Partial Volume" character
      ;;---
      (when (> percentage 0)
        (push (nth (floor (/ meter/char/partials/value 2)) meter/char/partials)
              output))

      ;;---
      ;; "No Volume" characters
      ;;---
      ;; Pad end of full/partial volume str with empties.
      (string-pad (if output
                      (apply #'string (nreverse output))
                    "")
                  meter/length
                  meter/char/empty)))
  ;; (length (int<smudge>:title:volume/meter 0))
  ;; (int<smudge>:title:volume/meter 1)
  ;; (int<smudge>:title:volume/meter 10)
  ;; (int<smudge>:title:volume/meter 11)
  ;; (int<smudge>:title:volume/meter 44)


  (defun int<smudge>:title:detailed ()
    "Output detailed (multi-line) player status string"
    ;; TODO: Make a helper in `smudge' for getting things from inside of this status.
    ;; https://developer.spotify.com/documentation/web-api/reference/get-the-users-currently-playing-track
    (if-let ((json/status (smudge-cache-api-get-status :name (system-name))))
        (let* (;;---
               ;; JSON Fields & Player State
               ;;---
               (state/playing (gethash 'is_playing json/status)) ; :json-false... t?
               (state/repeat (gethash 'repeat_state json/status)) ; "off", "track", "context"
               (state/shuffle (gethash 'shuffle_state json/status)) ; :json-false... t?
               (state/progress/ms (gethash 'progress_ms json/status))
               (json/item (gethash 'item json/status))
               (state/duration/ms (gethash 'duration_ms json/item))
               (state/name/track (gethash 'name json/item))
               ;; `item' is a 'TrackObject' _or_ an 'EpisodeObject'.
               ;; Assume 'TrackObject'.
               ;; TODO: Be smarter; figure it out/error.
               (json/album (gethash 'album json/item))
               (state/name/album (gethash 'name json/album))
               (list/artists (gethash 'artists json/item)) ; list of 'ArtistObject'
               (state/name/artist/count (length list/artists))
               (state/name/artist/primary (gethash 'name (nth 0 list/artists)))
               (state/name/artist/all (string-join (mapcar (lambda (artist-json)
                                                             (gethash 'name artist-json))
                                                           list/artists)
                                                   ", "))
               (state/name/artist (if (> state/name/artist/count 1)
                                      state/name/artist/all
                                    state/name/artist/primary))
               (json/device (gethash 'device json/status))
               (state/name/device (gethash 'name json/device))
               ;; TODO: Name the device if not this device...
               ;; But before that we'd need to be able to get status without a device name/id.
               ;; See `smudge-cache-api-get-status' at top of this `let*'.
               (state/device/this? (and (stringp state/name/device)
                                        (string= state/name/device (system-name))))
               (state/volume/percent (gethash 'volume_percent json/device))
               (state/volume/meter (if (integerp state/volume/percent)
                                       (int<smudge>:title:volume/meter state/volume/percent)
                                     ""))

               ;;---
               ;; Row Titles
               ;;---
               (row/artist (concat "Artist"
                                   (when (> 1 state/name/artist/count)
                                     "s")))
               (row/album "Album")
               (row/track "Track")
               (row/status "Status")
               (row/volume "Volume")
               (row/width/title (max (length row/artist)
                                     (length row/album)
                                     (length row/track)
                                     (length row/status)
                                     (length row/volume)))
               ;;---
               ;; Formatting
               ;;---
               (row/indent/standard "  ‚îú‚îÄ")
               (row/indent/final    "  ‚îî‚îÄ")
               (row/indent/fill     "‚îÄ")

               (format/row/title (concat "%"
                                         (number-to-string row/width/title)
                                         "s"))

               (row/artist (string-replace " " "‚îÄ" (format format/row/title row/artist)))
               (row/album  (string-replace " " "‚îÄ" (format format/row/title row/album)))
               (row/track  (string-replace " " "‚îÄ" (format format/row/title row/track)))
               (row/status (string-replace " " "‚îÄ" (format format/row/title row/status)))
               (row/volume (string-replace " " "‚îÄ" (format format/row/title row/volume)))

               (format/row "%s%s: %s"))
          ;;------------------------------
          ;; Put it all together prettily.
          ;;------------------------------
          (concat "‚îÄ‚îÄ‚î¨‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ\n"
                  (mapconcat (lambda (entry)
                               ;; Format the status lines:
                               (apply #'format format/row entry))
                             (list (list row/indent/standard row/artist state/name/artist)
                                   (list row/indent/standard row/album  state/name/album)
                                   (list row/indent/standard row/track  state/name/track)
                                   (list row/indent/standard
                                         row/status
                                         (concat
                                          (if (eq state/playing :json-false) "‚è∏" "‚ñ∂Ô∏è")
                                          (cond ((string= "track" state/repeat)
                                                 "üîÇ")
                                                ((string= "context" state/repeat)
                                                 "üîÅ")
                                                (t ;; Should just be "off" but catch other unexpected values.
                                                 nil))
                                          (unless (eq state/shuffle :json-false) "üîÄ")))
                                   (list row/indent/final
                                         row/volume
                                         (format "%s %d%%"
                                                 state/volume/meter
                                                 state/volume/percent)))
                             "\n")))
      ;; Else there is no status currently...
      nil))
  ;; (int<smudge>:title:detailed)


  ;; Could have plist args if I need to customize?
  (defun int<smudge>:title ()
    "Get title string for `pretty-hydra'."
    ;; This is used in `pretty-hydra-define', so we have to escape any
    ;; percentages in order to make it through `pretty-hydra's string
    ;; formatting.
    (string-replace "%" "%%"
                    (mapconcat #'identity
                               (list
                                ;; Icon & "Spotify"
                                int<smudge>:hydra/title

                                ;; Status?
                                (pcase int<smudge>:hydra/title:status/style
                                  ;; Short Status: Player Status formatted string.
                                  ('status
                                   smudge-controller-player-status)
                                  ;; Long Status: Table of status details.
                                  ('detailed
                                   (int<smudge>:title:detailed))
                                  ;; Shortest Status: No status.
                                  (_
                                   nil)))
                               "\n")))
  ;; (int<smudge>:title)


  ;; TODO: pretty-hydra needs to know about unicode character widths in order to make nice tables? :sad:
  ;;   - In the mean time, move the icon stuff to last in the columns.
  ;;
  ;; TODO: pretty-hydra: Would be nice if there was an `:enabled' that could grey things out & ignore keybind if not true.
  ;;
  ;; TODO: Update the 'now playing' status line more often?
  ;;   - Currently it only updates when a key is pressed, then the hydra is close/reopened.
  ;;   - Use `:formatter' arg/function?
  ;;     - https://github.com/jerrypnz/major-mode-hydra.el#pretty-hydra-define
  (pretty-hydra-define int<smudge>:hydra
    (:quit-key "g"
     :color blue ;; Default to `:exit t' for all heads.
     :title (int<smudge>:title))

    ;;------------------------------
    ;; Search: Playlists, Artist, etc.
    ;;------------------------------
    ("Search"
     (("h"
       smudge-my-playlists
       "My Playlists")

      ("t"
       smudge-featured-playlists
       "Featured Playlists")

      ("n"
       smudge-user-playlists
       "User Playlists")

      ("s"
       smudge-track-search
       "Search for Track"))

     ;;------------------------------
     ;; Manage: Devices
     ;;------------------------------
     "Manage"
     (("d" smudge-select-device
       "Select Device")

      ("i" (async<smudge>:init)
       ;; Can't have non-monospaced characters in this column... throws off the later columns.
       ;; "üöÄ Initialize"
       "Initialize"
       :exit nil)

      ("x" (mantle:workday:end/spotify)
       "Pause & Quit")

      ;; TODO: A 'refresh' which makes a call to get a new status string, then redraws the hydra when the result comes back?
      ;; "üîÑ Refresh Status"
      ;;   - TODO: Would putting a zero-width space in there make pretty-hydra align things correctly due to the double-wide glyph?
      )

     ;;------------------------------
     ;; Control: Play, Volume, etc.
     ;;------------------------------
     "Track"
     ;; TODO: Toggle/choose from play or pause: "‚ñ∂" "‚è∏"
     (("p"
       smudge-controller-toggle-play
       "‚èØ"
       :exit nil)
      ;; Could do?:
      ;;   :toggle #'func-that-gets-current-state
      ;;   :exit nil

      ("u" ;; character forward
       smudge-controller-next-track
       "‚è≠"
       :exit nil)

      ("o" ;; character backwards
       smudge-controller-previous-track
       "‚èÆ"
       :exit nil)

      (";"
       smudge-controller-toggle-shuffle
       "üîÄ"
       ;; TODO: :toggle #'func-that-gets-current-state
       )

      ("q"
       smudge-controller-toggle-repeat
       "üîÅ"
       ;; TODO: :toggle #'func-that-gets-current-state
       ))

     ;; NOTE: Wanted "Volume (42%)", but can't do a dynamic label here? :(
     "Volume"
     (("." ;; line previous
       smudge-controller-volume-up
       "üîä"
       ;; (format "üîä (%d%%)" (smudge-connect-volume-get-next (smudge-cache-get-volume :name (system-name)) +1)))
       ;; (concat "üîä ("
       ;;         (number-to-string (smudge-connect-volume-get-next (smudge-cache-get-volume :name (system-name)) +1))
       ;;         "%)" )
       :exit nil)

      ("e" ;; line next
       smudge-controller-volume-down
       "üîâ"
       ;;(format "üîâ (%d%%)" (smudge-connect-volume-get-next (smudge-cache-get-volume :name (system-name)) -1))
       :exit nil)

      ;; TODO: choose from mute or unmute: "üîá" "üîà"
      ("x"
       smudge-controller-volume-mute-unmute
       ;; NOTE: This doesn't work!!! It evaluates once and then fuck you?!
       ;; TODO: fix?
       (if (smudge-cache-api-is-muted :name (system-name))
           "üîà Unmute"
         "üîá Mute")
       :toggle (smudge-cache-api-is-muted :name (system-name))
       :exit nil))))
  ;; (int<smudge>:hydra/body)


  (defun int<smudge>:hydra-maybe-init ()
    "Initialize to this device if nothing is active, then open the hydra."
    (interactive)
    (unless smudge-selected-device-id
      (async<smudge>:init :device 'preserve))
    (int<smudge>:hydra/body))


  ;;------------------------------
  :general
  ;;------------------------------

  (keybind:leader/global:def
    :infix (keybind:infix "u") ; ...."apps & stuff"
    "u" (list #'int<smudge>:hydra-maybe-init :which-key (concat int<smudge>:hydra/title " Remote"))))


;;------------------------------------------------------------------------------
;; The End.
;;------------------------------------------------------------------------------
(imp:provide :mantle 'config 'spotify)
